
-- Raw elaboration-time conversion performance: check that Church-coded 10
-- million is equal to a different definition of itself.
--------------------------------------------------------------------------------

Nat : U
 = (n : U) → (n → n) → n → n

Eq : {A} → A → A → U
 = λ {A} x y. (P : A → U) → P x → P y

refl : {A}{x : A} → Eq x x
 = λ P px. px

n2 : Nat = λ n s z. s (s z)
n5 : Nat = λ n s z. s (s (s (s (s z))))

mul : Nat → Nat → Nat
 = λ a b n s z. a n (b n s) z

n10    = mul n2 n5
n100   = mul n10 n10
n10k   = mul n100 n100
n10kb  = mul n100 (mul n10 n10)
n1M    = mul n10k n100
n1Mb   = mul n100 n10k
n10M   = mul n1M n10
n10Mb  = mul n10 n1M

stress : Eq n10M n10Mb = refl